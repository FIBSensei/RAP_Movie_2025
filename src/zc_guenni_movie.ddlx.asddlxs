@Metadata.layer: #CUSTOMER

@UI.headerInfo: { typeName: 'Movie',
                  typeNamePlural: 'Movies',
                  title.value: 'Title',
                  description.value: 'PublishingYear',
                  imageUrl: 'ImageUrl' }

@UI.lineItem: [ { criticality: 'AverageRatingCriticality' } ]
@UI.presentationVariant: [ { sortOrder: [ { by: 'AverageRating', direction: #DESC } ] } ]

annotate entity ZC_GUENNI_Movie with

{
  /* Facets */
  @UI.facet: [ { position: 10, type: #FIELDGROUP_REFERENCE, label: 'Movie Details', targetQualifier: 'Details' },
               { position: 20, type: #FIELDGROUP_REFERENCE, label: 'Administrative Data', targetQualifier: 'Admin' },
               { position: 30, type: #LINEITEM_REFERENCE, label: 'Ratings', targetElement: '_Ratings' } ]
  // #LINEITEM_REFERENCE -> Stellt Association als Tabelle dar

  /* Fields */
  @UI.fieldGroup: [ { position: 10, qualifier: 'Admin' } ]
  MovieUuid;

  // fieldGroup -> In welcher Gruppe soll das Item angezeigt werden 
  @UI.fieldGroup: [ { position: 10, qualifier: 'Details' } ]
  // lineItem -> Wird auf Objektseite angezeigt
  @UI.lineItem: [ { position: 10 } ]
  Title;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 20 } ]
  // Wird als Filter angezeigt
  @UI.selectionField: [ { position: 10 } ]
  Genre;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Details' } ]
  // cssDefault.width -> Breite der Spalte
  @UI.lineItem: [ { position: 30, cssDefault.width: '7.2rem' } ]
  @UI.selectionField: [ { position: 20 } ]
  PublishingYear;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 40, cssDefault.width: '8.6rem' } ]
  @UI.selectionField: [ { position: 30 } ]
  RuntimeInMin;

  @UI.dataPoint: { visualization: #NUMBER, criticality: 'AverageRatingCriticality' }
  // dataPoint wird IMMER im Header angezeigt
  @UI.lineItem: [ { position: 50,
                    cssDefault.width: '8rem',
                    criticality: 'AverageRatingCriticality',
                    criticalityRepresentation: #WITHOUT_ICON } ]
  @UI.selectionField: [ { position: 999 } ]
  AverageRating;

  @UI.hidden: true
  AverageRatingCriticality;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Details' } ]
  @UI.lineItem: [ { position: 5, cssDefault.width: '5.7rem' } ]
  ImageUrl;

  @UI.fieldGroup: [ { position: 20, qualifier: 'Admin' } ]
  CreatedAt;

  @UI.fieldGroup: [ { position: 30, qualifier: 'Admin' } ]
  CreatedBy;

  @UI.fieldGroup: [ { position: 40, qualifier: 'Admin' } ]
  LastChangedAt;

  @UI.fieldGroup: [ { position: 50, qualifier: 'Admin' } ]
  LastChangedBy;

  @UI.lineItem: [ { position: 10 } ]
  _Ratings;
}